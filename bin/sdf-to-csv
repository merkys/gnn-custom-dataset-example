#!/usr/bin/perl

use strict;
use warnings;

use Chemistry::Mol;
use Chemistry::File::SDF;
use File::Basename qw( basename );

my $program = basename $0;

@ARGV = ( '-' ) unless @ARGV;

Chemistry::Mol->register_format( sdf => Chemistry::File::SDF:: );

$\ = "\n";
$, = ',';

sub id_to_number($);

my @all_stems;
foreach my $filename (@ARGV) {
    local $SIG{__WARN__} = sub {
        print STDERR "$program: $filename: $_[0]"
    };

    eval {
        my $reader = Chemistry::Mol->file( $filename, format => 'sdf' );
        $reader->open( '<' );
        my $n = 0;
        Chemistry::Atom::reset_id();
        while( my $molecule = $reader->read_mol( $reader->fh ) ) {
            my $dir = 'input_graph_CSV_files/data/' . basename( $filename, '.sdf' );
            `mkdir --parents $dir`;
            open( my $EDGES, '>', $dir . '/' . basename( $filename, '.sdf' ) . '_' . $n . '_edges.csv' );
            open( my $VERTICES_IN, '>', $dir . '/' . basename( $filename, '.sdf' ) . '_' . $n . '_vertices_in.csv' );
            open( my $VERTICES_OUT, '>', $dir . '/' . basename( $filename, '.sdf' ) . '_' . $n . '_vertices_out.csv' );
            push @all_stems, './data/' . basename( $filename, '.sdf' ) . '/' . basename( $filename, '.sdf' ) . '_' . $n;

            print $EDGES 'from', 'to', 'length';
            print $VERTICES_IN 'id', 'mass';
            print $VERTICES_OUT 'id', 'bond_order_sum';

            # Print out the vertices
            for my $atom ($molecule->atoms) {
                print $VERTICES_IN id_to_number $atom->id, $atom->mass;
                my $bond_order_sum = 0;
                for my $bond ($atom->bonds) {
                    $bond_order_sum += $bond->order;
                }
                print $VERTICES_OUT id_to_number $atom->id, $bond_order_sum;
                print $EDGES id_to_number $atom->id, id_to_number $atom->id, 0;
            }

            for my $bond ($molecule->bonds) {
                my @atoms = map { id_to_number $_ } $bond->atoms;
                # Edges must go to both directions
                print $EDGES @atoms, $bond->length;
                print $EDGES reverse( @atoms ), $bond->length;
            }

            close $EDGES;
            close $VERTICES_IN;
            close $VERTICES_OUT;

            $n++;
        }
    };
    if( $@ ) {
        $@ =~ s/\n$//;
        print STDERR "$program: $filename: $@\n";
    }

}

open( my $TRAIN, '>', 'input_graph_CSV_files/training_data_files_prefixes.txt' );
open( my $TEST, '>', 'input_graph_CSV_files/testing_data_files_prefixes.txt' );
open( my $VALID, '>', 'input_graph_CSV_files/validation_data_files_prefixes.txt' );

for my $stem (@all_stems) {
    my $rand = rand;
    if( $rand < 4/6 ) {
        print $TRAIN $stem;
    } elsif( $rand < 5/6 ) {
        print $TEST $stem;
    } else {
        print $VALID $stem;
    }
}

close $TRAIN;
close $TEST;
close $VALID;

sub id_to_number($)
{
    my( $id ) = @_;
    $id =~ s/^a//;
    $id--;
    return $id;
}
